/****************************************************
			   DAMAGE PROCS
****************************************************/

/obj/item/organ/external/proc/is_damageable(additional_damage = 0)
	//Continued damage to vital organs can kill you, and robot organs don't count towards total damage so no need to cap them.
	return (BP_IS_ROBOTIC(src) || brute_dam + burn_dam + additional_damage < max_damage * 4)

obj/item/organ/external/take_general_damage(amount, silent = FALSE)
	take_external_damage(amount)


// Deals blunt damage, distributes 50% of the damage between cut and pierce if there's excessive damage.
/obj/item/organ/external/proc/take_blunt_damage(brute)
	if(owner && (owner.status_flags & GODMODE))
		return

	// The maximum amount of damage of each type we can physically inflict.
	var/max_blunt_damage = brute
	var/max_cut_damage = brute * 0.25
	var/max_pierce_damage = brute * 0.25

	// The amount of damage we'd like to inflict.
	var/potential_blunt_damage = brute
	var/potential_cut_damage = 0
	var/potential_pierce_damage = 0

	var/damage_potential = 0

	if(blunt_dam < max_damage)
		damage_potential = max_damage - blunt_dam
		max_blunt_damage = min(brute, damage_potential)
		if(damage_potential < potential_blunt_damage)
			potential_cut_damage += potential_blunt_damage - damage_potential
			potential_pierce_damage += potential_blunt_damage - damage_potential
	else
		max_blunt_damage = 0
		potential_cut_damage += potential_blunt_damage * 0.25
		potential_pierce_damage += potential_blunt_damage * 0.25

	var/final_blunt_damage = min(potential_blunt_damage, max_blunt_damage)
	blunt_dam = min(blunt_dam + final_blunt_damage, max_damage)

	var/final_cut_damage = min(potential_cut_damage, max_cut_damage)
	if(final_cut_damage >= 2.5)
		cut_dam = min(cut_dam + final_cut_damage, max_damage)

	var/final_pierce_damage = min(potential_pierce_damage, max_pierce_damage)
	if(final_cut_damage >= 2.5)
		pierce_dam = min(pierce_dam + final_pierce_damage, max_damage)

	return

// Deals 75% of the amount as cut damage and 25% as pierce damage, deals up to 50% as blunt if excessive.
/obj/item/organ/external/proc/take_cut_damage(brute)
	if(owner && (owner.status_flags & GODMODE))
		return

	// The maximum amount of damage of each type we can physically inflict.
	var/max_pierce_damage = brute
	var/max_cut_damage = brute
	var/max_blunt_damage = brute * 0.5

	// The amount of damage we'd like to inflict.
	var/potential_pierce_damage = brute * 0.25
	var/potential_cut_damage = brute * 0.75
	var/potential_blunt_damage = 0

	var/damage_potential = 0

	if(cut_dam < max_damage)
		damage_potential = max_damage - cut_dam
		max_cut_damage = min(brute, damage_potential)
		if(damage_potential < potential_cut_damage)
			potential_pierce_damage += potential_cut_damage - damage_potential
	else
		max_cut_damage = 0
		potential_pierce_damage += potential_cut_damage

	if(pierce_dam < max_damage)
		damage_potential = max_damage - pierce_dam
		max_pierce_damage = min(brute, damage_potential)
		if(damage_potential < potential_pierce_damage)
			potential_cut_damage += potential_pierce_damage - damage_potential
	else
		max_pierce_damage = 0
		potential_cut_damage += potential_pierce_damage

	var/final_cut_damage = min(potential_cut_damage, max_cut_damage)
	cut_dam = min(cut_dam + final_cut_damage, max_damage)

	var/final_pierce_damage = min(potential_pierce_damage, max_pierce_damage)
	pierce_dam = min(pierce_dam + final_pierce_damage, max_damage)

	var/final_blunt_damage = clamp((brute - final_pierce_damage - final_cut_damage) * 0.5, 0, max_blunt_damage)
	blunt_dam = min(blunt_dam + final_blunt_damage, max_damage)

	return

// Deals 25% of the amount as cut damage and 75% as pierce damage, deals up to 50% as blunt if excessive.
/obj/item/organ/external/proc/take_pierce_damage(brute)
	if(owner && (owner.status_flags & GODMODE))
		return

	// The maximum amount of damage of each type we can physically inflict.
	var/max_blunt_damage = brute * 0.5
	var/max_cut_damage = brute
	var/max_pierce_damage = brute

	// The amount of damage we'd like to inflict.
	var/potential_blunt_damage = 0
	var/potential_cut_damage = brute * 0.25
	var/potential_pierce_damage = brute * 0.75

	var/damage_potential = 0

	if(pierce_dam < max_damage)
		damage_potential = max_damage - pierce_dam
		max_pierce_damage = min(brute, damage_potential)
		if(damage_potential < potential_pierce_damage)
			potential_cut_damage += potential_pierce_damage - damage_potential
	else
		max_pierce_damage = 0
		potential_cut_damage += potential_pierce_damage

	if(cut_dam < max_damage)
		damage_potential = max_damage - cut_dam
		max_cut_damage = min(brute, damage_potential)
		if(damage_potential < potential_cut_damage)
			potential_pierce_damage += potential_cut_damage - damage_potential
	else
		max_cut_damage = 0
		potential_pierce_damage += potential_cut_damage

	var/final_pierce_damage = min(potential_pierce_damage, max_pierce_damage)
	pierce_dam = min(pierce_dam + final_pierce_damage, max_damage)

	var/final_cut_damage = min(potential_cut_damage, max_cut_damage)
	cut_dam = min(cut_dam + final_cut_damage, max_damage)

	var/final_blunt_damage = clamp((brute - final_pierce_damage - final_cut_damage) * 0.5 , 0, max_blunt_damage)
	blunt_dam = min(blunt_dam + final_blunt_damage, max_damage)

	return

/obj/item/organ/external/proc/take_burn_damage(burn)
	if(owner && (owner.status_flags & GODMODE))
		return

	if(burn_dam >= max_damage * 2.0)
		return

	burn_dam = min(max_damage * 2.0, burn_dam + burn)

	return

/obj/item/organ/external/proc/cache_last_damage()
	blunt_last = blunt_dam
	cut_last = cut_dam
	pierce_last = pierce_last

	brute_last = brute_dam
	burn_last = burn_dam

/obj/item/organ/external/proc/take_external_damage(brute, burn, damage_flags, used_weapon = null)
	if(owner && (owner.status_flags & GODMODE))
		return 0

	brute = round(brute * brute_mod, 0.1)
	burn = round(burn * burn_mod, 0.1)

	if(brute <= 0 && burn <= 0)
		return 0

	var/sharp = (damage_flags & DAM_SHARP)
	var/edge  = (damage_flags & DAM_EDGE)
	var/laser = (damage_flags & DAM_LASER)
	var/blunt = brute && !sharp && !edge

	if(used_weapon)
		add_autopsy_data("[used_weapon]", brute + burn)

	if(owner) // No need to report damage inflicted on severed limbs
		if(brute)
			SSstoryteller.report_wound(owner, BRUTE, brute)
		if(burn)
			SSstoryteller.report_wound(owner, BURN, burn)

	cache_last_damage()

	if(brute)
		if(blunt)
			take_blunt_damage(brute)
		else if(edge)
			take_cut_damage(brute)
		else if(sharp)
			take_pierce_damage(brute)
		brute_dam = blunt_dam + cut_dam + pierce_dam
		brute_ratio = brute_dam / max_damage

	if(burn)
		take_burn_damage(burn)
		burn_ratio = burn_dam / max_damage
		if(laser && prob(40))
			owner?.IgniteMob()

	var/blunt_dealt = blunt_dam - blunt_last
	var/cut_dealt = cut_dam - cut_last
	var/pierce_dealt = pierce_dam - pierce_last

	var/brute_dealt = brute_dam - brute_last
	var/burn_dealt = burn_dam - burn_last

	// Dismemberment stuff
	if(!isnull(owner)) && loc == owner)
		owner.updatehealth()
		if((limb_flags & ORGAN_FLAG_CAN_AMPUTATE) && config.health.limbs_can_break && !is_stump())
			var/force_droplimb = FALSE
			if(brute >= 5.0 && (brute_last + brute >= max_damage * 3) || burn && (burn_dam >= max_damage * 2))
				force_droplimb = TRUE

			if(burn && (burn_dam >= max_damage))
				if(prob(burn) || force_droplimb))
					droplimb(laser, DROPLIMB_BURN)
					return

			if(edge && (cut_dam >= max_damage))
				if((brute >= 10.0 && prob(brute * brute_ratio)) || force_droplimb)
					droplimb(FALSE, DROPLIMB_EDGE)
					return

			if(sharp && !edge && (pierce_dam >= max_damage))
				if((brute >= 10.0 && prob(brute * brute_ratio)) || force_droplimb)
					droplimb(FALSE, pick(DROPLIMB_EDGE, DROPLIMB_BLUNT))
					return

			if(blunt && (blunt_last >= max_damage) && (status & ORGAN_BROKEN))
				if((brute >= (min_broken_damage * 0.5) && prob(brute * brute_ratio)) || force_droplimb)
					droplimb(FALSE, DROPLIMB_BLUNT)
					return

			if(force_droplimb)
				droplimb(FALSE, DROPLIMB_BLUNT)
				return

	// High brute damage or sharp objects may damage internal organs
	if(!istype(used_weapon, /obj/item/projectile)) // Projectiles organ damage is being processed in human_defense.dm
		var/damage_amt = brute
		var/cur_damage = brute_last
		if(laser)
			damage_amt += burn
			cur_damage += burn_last
		var/organ_damage_threshold = 5
		if(sharp)
			organ_damage_threshold *= 0.5
		var/organ_damage_prob = 6.25 * damage_amt/organ_damage_threshold //more damage, higher chance to damage
		if(sharp)
			organ_damage_prob *= 1.5
		if(cur_damage >= 15)
			organ_damage_prob *= cur_damage/15
		if(encased && !(status & ORGAN_BROKEN)) //ribs and skulls protect
			organ_damage_prob *= 0.5
		if(LAZYLEN(internal_organs) && (cur_damage + damage_amt >= max_damage || damage_amt >= organ_damage_threshold) && prob(organ_damage_prob))
			// Damage an internal organ
			var/list/victims = list()
			for(var/obj/item/organ/internal/I in internal_organs)
				if(I.damage < I.max_damage && prob(I.relative_size))
					victims += I
			if(!victims.len)
				victims += pick(internal_organs)
			for(var/obj/item/organ/internal/victim in victims)
				brute /= 2
				if(laser)
					burn /= 3
				damage_amt /= 2
				victim.take_internal_damage(damage_amt)

	if(!BP_IS_ROBOTIC(src))
		// Painful stuff
		adjust_pain(0.6 * burn + 0.4 * brute)

		// Bones stuff
		if(brute >= (blunt ? 5.0 : 10.0))
			if(status & ORGAN_BROKEN)
				jostle_bone(brute)
				if(owner && prob(40) && can_feel_pain())
					owner.emote("scream") // Getting hit on a broken hand hurts
			else
				var/should_fracture = FALSE
				if(blunt_last >= max_damage && (blunt || prob(brute_dam + brute)))
					should_fracture = TRUE
				else if(!should_fracture && blunt_dam >= min_broken_damage)
					if(prob(brute_dam + brute * (1 + blunt))) // blunt damage is gud at fracturing
						should_fracture = TRUE

				if(should_fracture)
					fracture()

		// Bloody stuff
		if(brute)
			if(edge)
				bandaged -= brute
				scabbed -= brute
			else
				bandaged -= brute * 0.5
				scabbed -= brute * 0.5

		if(burn)
			bandaged -= burn
			scabbed += burn // Cauterization

			// Burn damage can cause fluid loss due to blistering and cook-off.
			// Smaller limbs and existing bloodloss reduce the amount of fluid loss.
			if(owner && (burn_dam >= min_broken_damage))
				var/fluid_loss = ceil((damage/(owner.maxHealth - config.health.health_threshold_dead)) * owner.species.blood_volume * (max_damage / 100) * (min(10, owner.get_blood_volume()) / 100))
				owner.remove_blood(fluid_loss  * (laser ? FLUIDLOSS_CONC_BURN : FLUIDLOSS_WIDE_BURN))

		// Arteries+Tendons stuff
		if(brute > 15 && max(cut_dam, pierce_dam) > min_broken_damage)
			var/internal_damage
			if(prob(ceil(damage/2)) && sever_artery())
				internal_damage = TRUE
			if(prob(ceil(damage/4)) && sever_tendon())
				internal_damage = TRUE
			if(internal_damage)
				owner.custom_pain("You feel something rip in your [name]!", 50, affecting = src)

	// Sync the organ's damage with its wounds
	update_damages()

	if(owner)
		owner.updatehealth()
		if(update_damstate())
			owner.UpdateDamageIcon()
		else if(status & ORGAN_BLEEDING)
			owner.update_bandages() // TODO: Rework bandages

	return created_wound

/obj/item/organ/external/heal_damage(brute, burn, internal = 0, robo_repair = 0, update_damage_icon = TRUE)
	if(BP_IS_ROBOTIC(src) && !robo_repair)
		return FALSE

	if(burn_dam && burn && (burn_ratio < 1 || vital || (limb_flags & ORGAN_FLAG_HEALS_OVERKILL)))
		burn = min(burn_dam, burn)
		burn_dam -= burn
		owner?.heal_this_tick += burn

	if(brute_dam && brute && (brute_ratio < 1 || vital || (limb_flags & ORGAN_FLAG_HEALS_OVERKILL)))
		// Healing random brute-based damage, while trying not to "spill" healing.
		var/healed_amount
		var/type_to_heal
		var/list/damaged_types = list(&pierce_dam, &cut_dam, &blunt_dam)
		while(brute > 0 && brute_dam > 0 && damaged_types.len)
			type_to_heal = pick(damaged_types)
			if(*type_to_heal <= 0)
				damaged_types -= type_to_heal
				continue

			healed_amount = min(brute, *type_to_heal)

			*type_to_heal -= healed_amount
			brute -= healed_amount
			owner?.heal_this_tick += healed_damage
			if(*type_to_heal < 0.1)
				*type_to_heal = 0
				damaged_types -= type_to_heal
			else
				*type_to_heal = round(*type_to_heal, 0.1)

			brute_dam = pierce_dam + cut_dam + blunt_dam

	if(internal)
		mend_fracture(TRUE)

	//Sync the organ's damage with its wounds
	update_damages()
	owner.updatehealth()

	var/should_update_damstate = update_damstate()
	if(owner && update_damage_icon && should_update_damstate)
		owner.UpdateDamageIcon()

	return should_update_damstate

// Brute/burn
/obj/item/organ/external/proc/get_brute_damage()
	return brute_dam

/obj/item/organ/external/proc/get_burn_damage()
	return burn_dam

// Geneloss/cloneloss.
/obj/item/organ/external/proc/get_genetic_damage()
	return (BP_IS_ROBOTIC(src) || (species?.species_flags & SPECIES_FLAG_NO_SCAN)) ? 0 : genetic_degradation

/obj/item/organ/external/proc/remove_genetic_damage(amount)
	if((species.species_flags & SPECIES_FLAG_NO_SCAN) || BP_IS_ROBOTIC(src))
		genetic_degradation = 0
		status &= ~ORGAN_MUTATED
		return
	var/last_gene_dam = genetic_degradation
	genetic_degradation = min(100,max(0,genetic_degradation - amount))
	if(genetic_degradation <= 30)
		if(status & ORGAN_MUTATED)
			unmutate()
			to_chat(src, "<span class = 'notice'>Your [name] is shaped normally again.</span>")
	return -(genetic_degradation - last_gene_dam)

/obj/item/organ/external/proc/add_genetic_damage(amount)
	if(owner.status_flags & GODMODE)
		return 0
	if((species.species_flags & SPECIES_FLAG_NO_SCAN) || BP_IS_ROBOTIC(src))
		genetic_degradation = 0
		status &= ~ORGAN_MUTATED
		return
	var/last_gene_dam = genetic_degradation
	genetic_degradation = min(100,max(0,genetic_degradation + amount))
	if(genetic_degradation > 30)
		if(!(status & ORGAN_MUTATED) && prob(genetic_degradation))
			mutate()
			to_chat(owner, "<span class = 'notice'>Something is not right with your [name]...</span>")
	return (genetic_degradation - last_gene_dam)

/obj/item/organ/external/proc/mutate()
	if(BP_IS_ROBOTIC(src))
		return
	src.status |= ORGAN_MUTATED
	if(owner)
		owner.update_body()

/obj/item/organ/external/proc/unmutate()
	src.status &= ~ORGAN_MUTATED
	if(owner)
		owner.update_body()

/obj/item/organ/external/proc/update_pain()
	if(!can_feel_pain())
		pain = 0
		full_pain = 0
		return

	if(pain)
		pain -= (pain > max_damage ? 2.5 : 1) * (owner.lying ? 3 : 1) // Over-limit pain decreases faster.
		pain = max(pain, 0)

	var/lasting_pain = 0
	if(is_broken())
		lasting_pain += 10
	else if(is_dislocated())
		lasting_pain += 5

	full_pain = min(pain, max_damage) + lasting_pain + min(max_damage, 0.7 * brute_dam + 0.8 * burn_dam) + 0.5 * get_genetic_damage()

/obj/item/organ/external/proc/get_pain()
	return pain

/obj/item/organ/external/proc/adjust_pain(change)
	if(!can_feel_pain())
		return 0
	var/last_pain = pain
	pain = clamp(pain + change, 0, max_pain)
	full_pain += pain - last_pain // Updating it without waiting for the next tick for the greater good

	if(change > 0 && owner)
		owner.full_pain += pain - last_pain
		if((change > 15 && prob(20)) || (change > 30 && prob(60)))
			owner.emote("scream")

	return pain - last_pain

/obj/item/organ/external/proc/remove_all_pain()
	pain = 0
	owner?.full_pain -= full_pain
	full_pain = 0

/obj/item/organ/external/proc/get_default_pain_message(power)
	var/burning = burn_dam > brute_dam
	switch(power)
		if(1 to 5)
			return "Your [name] [burning ? "burns" : "hurts"] a bit."
		if(5 to 15)
			return "Your [name] [burning ? "burns" : "hurts"] slightly."
		if(15 to 25)
			return "Your [name] [burning ? "burns" : "hurts"]."
		if(25 to 90)
			return "Your [name] [burning ? "burns" : "hurts"] badly!"
		if(90 to INFINITY)
			return "OH GOD! Your [name] is [burning ? "on fire" : "hurting terribly"]!"

/obj/item/organ/external/proc/stun_act(stun_amount, agony_amount)
	if(owner.status_flags & GODMODE)
		return 0
	if(agony_amount > 5 && owner)

		if((limb_flags & ORGAN_FLAG_CAN_GRASP) && prob(25))
			owner.grasp_damage_disarm(src)

		if((limb_flags & ORGAN_FLAG_CAN_STAND) && prob(min(agony_amount * ((body_part == LEG_LEFT || body_part == LEG_RIGHT)? 1 : 2), 70)))
			owner.stance_damage_prone(src)

		if(vital && full_pain > 0.5 * max_damage)
			owner.visible_message("<b>[owner]</b> reels in pain!")
			if(has_genitals() || full_pain + agony_amount > max_damage)
				owner.Weaken(6)
			else
				owner.drop_l_hand()
				owner.drop_r_hand()
			owner.Stun(6)
			return 1

/obj/item/organ/external/proc/get_agony_multiplier()
	return has_genitals() ? 2 : 1

/obj/item/organ/external/proc/sever_artery()
	if(species && species.has_organ[BP_HEART])
		var/obj/item/organ/internal/heart/O = species.has_organ[BP_HEART]
		if(!BP_IS_ROBOTIC(src) && !(status & ORGAN_ARTERY_CUT) && !initial(O.open))
			status |= ORGAN_ARTERY_CUT
			return TRUE
	return FALSE

/obj/item/organ/external/proc/sever_tendon()
	if((limb_flags & ORGAN_FLAG_HAS_TENDON) && !BP_IS_ROBOTIC(src) && !(status & ORGAN_TENDON_CUT))
		status |= ORGAN_TENDON_CUT
		return TRUE
	return FALSE
